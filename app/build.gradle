apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    defaultConfig {
        applicationId "com.kimchangyoun.rootbeerFresh.sample"
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode 11
        versionName "0.0.11"
        vectorDrawables.useSupportLibrary = true

        archivesBaseName = "rootbeerFresh-$versionName-[$versionCode]"
    }

    //check if the keystore details are defined in gradle.properties (this is so the key is not in github)
    if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
        signingConfigs {
            //from ~/user/.gradle/gradle.properties
            release {
                storeFile file(ROOTBEER_SAMPLE_STORE)
                keyAlias ROOTBEER_SAMPLE_KEY
                storePassword ROOTBEER_SAMPLE_STORE_PASS
                keyPassword ROOTBEER_SAMPLE_KEY_PASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            if (project.hasProperty("ROOTBEER_SAMPLE_STORE")) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    namespace 'com.kimchangyoun.rootbeerFresh.sample'
}

dependencies {
    implementation project(':LibRootbeerFresh')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"

    implementation "androidx.core:core-ktx:$corektxVersion"

    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "com.google.android.material:material:$materialDesignVersion"

    implementation "com.nineoldandroids:library:$nineoldandroidsVersion"
    implementation "uk.co.barbuzz:beerprogressview:$beerProgressViewVersion"

    implementation "com.jakewharton.timber:timber:$timberVersion"

    implementation "com.android.support:multidex:1.0.3"
}
